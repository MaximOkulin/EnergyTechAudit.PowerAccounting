-- ====================================================================================================
-- Author:  	Leo
-- Create date: 2015-11-17
-- Description: Генерирует скрипт проверки отсутствующих ключей и индексов в рабочей базе данных 
--              на основе эталонной базы данных (из деплоя проекта)
-- ===================================================================================================

DECLARE @crlf NCHAR(2) = CHAR(13) + CHAR(10);
DECLARE @twiceCrlf NCHAR(4) = @crlf + @crlf;
DECLARE @tab NCHAR(1) = CHAR(9);
DECLARE @twiceTab NCHAR(2) = CHAR(9) + CHAR(9);

DECLARE @script NVARCHAR(MAX) = '';

SELECT @script = @script + @crlf +

  @tab + 'IF NOT EXISTS' + @crlf +
  @tab + '(' + @crlf + 
  IIF
  (
    [Type] = 'F',
    
      @twiceTab + 'SELECT * FROM [sys].[foreign_keys] [foreign_keys]' + @crlf + 
      @twiceTab + 'WHERE [foreign_keys].[object_id] = OBJECT_ID(N''[' + [Schema] + '].[' + [Key] +']'')' + + @crlf + 
      @twiceTab + 'AND [foreign_keys].[parent_object_id] = OBJECT_ID(N''[' + [Schema] + '].[' + [TableName] + ']'')',

      @twiceTab + 'SELECT * FROM [sys].[indexes] [indexes]' + @crlf + 
      @twiceTab + 'WHERE [indexes].[object_id] = OBJECT_ID(N''[' + [Schema] + '].[' + [TableName] + ']'')' + @crlf + 
      @twiceTab + 'AND [indexes].[name] = ''' + [Key] + ''''

  ) + @crlf +
  @tab + ')' + @crlf + 

  @tab + 'BEGIN' + @crlf + 
    @twiceTab + 'SET @missingConstraintsCount = @missingConstraintsCount + 1;' + @crlf + 
    @twiceTab + 'SET @missingConstraintsMessage = @missingConstraintsMessage + ' + '''' + 
      IIF
      (
        [Type] = 'F', 
        N'Внешний ключ: ' + '[' + [Schema] + '].[' + [Key] +']',
        N'Индекс: ' + [Key]
      )
      + N' отсутствует в таблице [' + [Schema] + '].[' + [TableName] + '].'' + @crlf;' + @crlf + 
  @tab + 'END;' + @twiceCrlf
  
  FROM 
  (
    (
      SELECT       
        SCHEMA_NAME([tables].[schema_id]) [Schema],
        [tables].[name] [TableName],
        [indexes].[name] [Key],
        'U' [Type]
      FROM 
        [sys].[indexes] [indexes]
      INNER JOIN 
        [sys].[tables] [tables] ON [indexes].[object_id] = [tables].[object_id]
    
      WHERE 
         [indexes].[is_primary_key] = 0      
         AND [tables].[is_ms_shipped] = 0
         AND [tables].[schema_id] != 1   
    )
    UNION ALL
    (
      SELECT 
        SCHEMA_NAME([foreign_keys].[schema_id]) [Schema],
        OBJECT_NAME([foreign_keys].[parent_object_id]) [TableName],
        [foreign_keys].[name] [Key],  
        'F' [Type] 
      FROM [sys].[foreign_keys] [foreign_keys]    
    )
  ) [Keys]
ORDER BY [Keys].[TableName];

-- DECLARE @output NVARCHAR(MAX);

SELECT 
  '-- ====================================================================================================' + @crlf +
  '-- Author:  	Leo (auto generated by BuildMissingConstraintsScript)' + @crlf +
  '-- Create date: 2015-11-17' + @crlf +
  '-- Description: Проверка потерянных ключей и индексов' + @crlf +
  '-- ===================================================================================================' + @crlf +
  'CREATE PROCEDURE [Programmability].[DoCheckMissingConstraints]' + @crlf + 
  'AS' + @crlf + 
  'BEGIN' + @crlf + 
  @tab + 'DECLARE @missingConstraintsCount INT = 0;' + @crlf +   
  @tab + 'DECLARE @missingConstraintsMessage NVARCHAR(MAX) = '''';' +  @crlf + 
  @tab + 'DECLARE @crlf NCHAR(2) = CHAR(13) + CHAR(10);' + 
  @twiceCrlf + 

  @tab + @script + 
  @twiceCrlf + 
  @tab + 'IF @missingConstraintsCount != 0' + @crlf + 
  @tab + 'BEGIN' + @crlf + 
  @twiceTab + 'DECLARE @serverName  NVARCHAR(128) = @@SERVERNAME;' + @crlf + 	
	@twiceTab + 'DECLARE @subject NVARCHAR(256) = N''Обнаружены отсутсвующие ключи на сервере '' + @serverName;' +@crlf + 
	@twiceTab + 'SET @missingConstraintsMessage = CONCAT(N''Найдены отсутствующие ключи: '',	@missingConstraintsCount, @crlf, @crlf, @missingConstraintsMessage);' +		@crlf + 
	  
  @twiceTab + 'EXEC msdb.dbo.sp_send_dbmail ' + @crlf + 
	@twiceTab + ' @profile_name = ''EnergyTechAudit.PowerAccounting.Database.MailProfile'',' + @crlf + 
	@twiceTab + ' @recipients = ''eta.development.dba@yandex.ru'',' + @crlf + 
	@twiceTab + ' @subject = @subject,' + @crlf +
  @twiceTab + ' @importance = ''High'',' + @crlf +
	@twiceTab + ' @body = @missingConstraintsMessage;' + @crlf + 
  @tab + 'END;' + @crlf +
  'END;'